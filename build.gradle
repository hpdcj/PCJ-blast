apply plugin: 'java'
apply from: 'jaxb.gradle'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
//compileJava.options.compilerArgs.add '-Xlint:unchecked'

repositories {
    mavenCentral()
    maven { url 'https://repository.cloudera.com/content/repositories/releases/' }
    maven { url 'https://repository.cloudera.com/cloudera/cloudera-repos' }
}

dependencies {
    compile files('../PCJ-libs/PCJ-5.0.6.jar')
    compile (group: 'org.apache.hadoop', name: 'hadoop-core', version: '2.6.0-mr1-cdh5.10.1') {
        transitive = false
    }
    compile (group: 'org.apache.hadoop', name: 'hadoop-common', version: '2.6.0-cdh5.10.1') {
        transitive = false
    }
    compile (group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: '2.6.0-cdh5.10.1') {
        transitive = false
    }
    annotationProcessor files('../PCJ-libs/PCJ-5.0.6.jar')
}

jaxb {
    inputs.file "src/main/resources/dtd/NCBI_BlastOutput.dtd"
    outputs.dir "${buildDir}/generated-src/"

    packageName = "ncbi.blast.result.generated"
}
    
compileJava {
    dependsOn jaxb
    source jaxb.outputs.files
}

sourceSets {
    main {
        java {
            srcDirs += [file("${buildDir}/generated-src/")]
        }
    }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.pcj.blast.Main'
}

tasks.withType(Jar) {
    destinationDir = file("${buildDir}/dist")
}

task copyToLib(type: Copy) {
    into "${buildDir}/dist/libs"
    from configurations.runtime
}

jar.dependsOn(copyToLib)

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Main-Class': 'org.pcj.blast.Main',
                   'Class-Path': configurations.runtime.files.collect { "libs/$it.name" }.join(' ')
    }
}
